apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'


android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "org.strongswan.android"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 48
        versionName "1.9.4"
    }

    productFlavors {
        strongswan {
            applicationId = "org.strongswan.android"
            minSdkVersion 21
            targetSdkVersion 22
            versionCode 48
            versionName "1.9.4"
        }
        strongswanProgetTest {
            applicationId = "test.vpn.enterprise.client"
            minSdkVersion 21
            targetSdkVersion 22
            versionCode 2
            versionName "1.0.1"
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }

    sourceSets.main {
        jni.srcDirs = [] // disables the default ndk-build call (with on-the-fly Android.mk files)
        jniLibs.srcDir 'src/main/libs'
    }

    task buildNative(type: Exec) {
        workingDir 'src/main/jni'
        commandLine "${android.ndkDirectory}/ndk-build", '-j', Runtime.runtime.availableProcessors()
    }

    task cleanNative(type: Exec) {
        workingDir 'src/main/jni'
        commandLine "${android.ndkDirectory}/ndk-build", 'clean'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    clean.dependsOn 'cleanNative'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
}

